buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath "jp.leafytree.gradle:gradle-android-scala-plugin:1.3.1"
    }
}

repositories {
    jcenter()
    maven {
        url 'http://JRAF.org/static/maven/2'
    }
    maven {
        url 'http://madeye-maven-repository.googlecode.com/git/'
    }
}

apply plugin: "com.android.application"
apply plugin: "jp.leafytree.android-scala"

android {
    compileSdkVersion "android-21"
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId 'com.github.shadowsocks'
        minSdkVersion 16
        targetSdkVersion 21
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
        versionCode 101
        versionName "2.6.3"
    }

    buildTypes {
        debug {
            //applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "2g"
    }
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.deprecation = false
    scalaCompileOptions.additionalParameters = ["-feature"]
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith("dex")
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += "--multi-dex"
        dx.additionalParameters += "--main-dex-list=$rootDir/main-dex-list.txt".toString()
    }
}

dependencies {
    // compile "com.android.support:multidex:1.0.0"
    compile "org.scala-lang:scala-library:2.10.4"
    // compile 'org.scalaj:scalaj-http_2.10:0.3.10'

    compile "dnsjava:dnsjava:2.1.6"
    compile 'com.github.kevinsawicki:http-request:5.6'
    compile "commons-net:commons-net:3.3"
    compile "com.google.zxing:android-integration:3.1.0"

    compile "net.simonvt.menudrawer:menudrawer:3.0.6"
    compile "com.google.android.gms:play-services-base:6.5.87"
    compile "com.google.android.gms:play-services-ads:6.5.87"

    compile "com.android.support:support-v4:21.0.3"

    compile "com.github.mrengineer13:snackbar:0.5.0"
    compile "com.nostra13.universalimageloader:universal-image-loader:1.8.4"
    compile "com.j256.ormlite:ormlite-core:4.47"
    compile "com.j256.ormlite:ormlite-android:4.47"
}

